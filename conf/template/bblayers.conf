# LAYER_CONF_VERSION is increased each time build/conf/bblayers.conf
# changes incompatibly
LCONF_VERSION = "7"
OEROOT := "${@os.path.abspath(os.path.dirname(d.getVar('FILE', True)))}/../.."

BBPATH = "${TOPDIR}"

BBFILES = ""

# These layers hold recipe metadata not found in OE-core, but lack any machine or distro content
BASELAYERS ?= " \
  ${OEROOT}/layers/meta-openembedded/meta-oe \
  ${OEROOT}/layers/meta-openembedded/meta-networking \
  ${OEROOT}/layers/meta-openembedded/meta-filesystems \
  ${OEROOT}/layers/meta-openembedded/meta-python \
  ${OEROOT}/layers/meta-openembedded/meta-perl \
  ${OEROOT}/layers/meta-virtualization \
  ${OEROOT}/layers/meta-updater \
"

# Dynamically included machine layers
RPI_LAYERS = " \
  ${OEROOT}/layers/meta-raspberrypi \
"
BEAGLEPLAY_LAYERS = " \
  ${OEROOT}/layers/meta-arm/meta-arm-toolchain \
  ${OEROOT}/layers/meta-arm/meta-arm \
  ${OEROOT}/layers/meta-ti/meta-ti-bsp \
  ${OEROOT}/layers/meta-ti/meta-ti-extras \
"
RISCV_LAYERS = " \
  ${OEROOT}/layers/meta-riscv \
"
YOCTOBSP_LAYERS = " \
  ${OEROOT}/layers/meta-yocto/meta-yocto-bsp \
"
INTEL_LAYERS = " \
  ${OEROOT}/layers/meta-intel \
"

# These layers hold machine specific content, aka Board Support Packages
BSPLAYERS ?= " \
  ${@'${RPI_LAYERS}' if '${MACHINE}'.startswith('raspberrypi') else ''} \
  ${@'${BEAGLEPLAY_LAYERS}' if '${MACHINE}' == 'beagleplay' else ''} \
  ${@'${RISCV_LAYERS}' if '${MACHINE}' == 'nezha-allwinner-d1' else ''} \
  ${@'${YOCTOBSP_LAYERS}' if '${MACHINE}' == 'genericx86-64' else ''} \
  ${@'${YOCTOBSP_LAYERS}' if '${MACHINE}' == 'beaglebone-yocto' else ''} \
  ${@'${INTEL_LAYERS}' if '${MACHINE}' == 'intel-corei7-64' else ''} \
  ${OEROOT}/layers/meta-yocto/meta-poky \
  ${OEROOT}/layers/meta-security \
"

# Add your overlay location to EXTRALAYERS
# Make sure to have a conf/layers.conf in there
EXTRALAYERS ?= ""

BBLAYERS = " \
  ${OEROOT}/layers/meta-common-torizon \
  ${OEROOT}/layers/meta-toradex-security \
  ${OEROOT}/layers/meta-toradex-distro \
  ${OEROOT}/layers/meta-toradex-bsp-common \
  ${BASELAYERS} \
  ${BSPLAYERS} \
  ${EXTRALAYERS} \
  ${OEROOT}/layers/openembedded-core/meta \
"

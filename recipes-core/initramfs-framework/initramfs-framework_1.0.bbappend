FILESEXTRAPATHS:prepend := "${THISDIR}/files:"

SRC_URI += "\
    file://plymouth \
    file://ostree \
    file://kmod \
    file://0001-Mount-run-with-tmpfs.patch \
    file://0002-only-scan-for-block-devices.patch \
"

SRC_URI:append:torizon-signed = "\
    file://composefs \
    file://80-composefs.conf \
"

PACKAGES:append = " \
    initramfs-module-plymouth \
    initramfs-module-ostree \
    initramfs-module-kmod \
"

PACKAGES:append:torizon-signed = "\
    initramfs-module-composefs \
"

SUMMARY:initramfs-module-plymouth = "initramfs support for plymouth"
RDEPENDS:initramfs-module-plymouth = "${PN}-base plymouth ${@bb.utils.contains('DISTRO_FEATURES', 'systemd', 'systemd-udev-rules', '', d)}"
FILES:initramfs-module-plymouth = "/init.d/02-plymouth"

SUMMARY:initramfs-module-ostree = "initramfs support for ostree based filesystems"
RDEPENDS:initramfs-module-ostree = "${PN}-base ostree-switchroot"
FILES:initramfs-module-ostree = "/init.d/95-ostree"

SUMMARY:initramfs-module-composefs = "initramfs support for booting composefs images"
RDEPENDS:initramfs-module-composefs = "${PN}-base kernel-module-composefs fsverity-utils util-linux-mount"
FILES:initramfs-module-composefs = "/init.d/98-composefs"

SUMMARY:initramfs-module-kmod = "initramfs support for loading kernel modules"
RDEPENDS:initramfs-module-kmod = "${PN}-base"
FILES:initramfs-module-kmod = "\
    /init.d/01-kmod \
    /etc/modules-load.d/* \
"

SUMMARY:initramfs-module-firmware = "initramfs support for HDMI firmware"
RDEPENDS:initramfs-module-firmware = "${PN}-base firmware-imx-hdmi"
FILES:initramfs-module-firmware = "\
    /usr/lib/firmware/* \
"

do_install:append() {
    install -m 0755 ${WORKDIR}/plymouth ${D}/init.d/02-plymouth
    install -m 0755 ${WORKDIR}/ostree ${D}/init.d/95-ostree
    install -m 0755 ${WORKDIR}/kmod ${D}/init.d/01-kmod
}

do_install:append:torizon-signed() {
    install -m 0755 ${WORKDIR}/composefs ${D}/init.d/98-composefs
    install -d ${D}/etc/modules-load.d/
    install -m 0755 ${WORKDIR}/80-composefs.conf ${D}/etc/modules-load.d/80-composefs.conf
}

SRC_URI:append:apalis-imx8 = " file://50-imx8-graphics.conf"
RDEPENDS:initramfs-module-kmod:append:apalis-imx8 = " \
    kernel-module-imx-gpu-viv \
    kernel-module-cdns-mhdp-imx \
"

do_install:append:apalis-imx8() {
    install -d ${D}/etc/modules-load.d/
    install -m 0755 ${WORKDIR}/50-imx8-graphics.conf ${D}/etc/modules-load.d/50-imx8-graphics.conf
}
